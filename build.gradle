buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.2'
        // Required plugins added to classpath to facilitate pushing to Jcenter/Bintray
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
    }
}

apply plugin: 'com.android.library'

ext {
    bintrayRepo = 'cc-android-sdk'
    bintrayName = 'cc-android-sdk'   // Has to be same as your library module name

    publishedGroupId = 'com.lotame'
    libraryName = 'LotameCrowdControlAndroidSDK'
    artifact = 'cc-android-sdk'     // Has to be same as your library module name

    libraryDescription = 'Lotame Platform Android SDK that enables sending data to Lotame from Android mobile apps.'

    // Your github repo link
    siteUrl = 'https://github.com/Lotame/cc-android-sdk'
    gitUrl = 'https://github.com/Lotame/cc-android-sdk.git'
    githubRepository= 'Lotame/cc-android-sdk'

    libraryVersionCode = 5
    libraryVersion = '2.3.0.2'

    developerId = 'Lotame'
    developerName = 'Lotame'
    developerEmail = 'info@lotame.com'

    licenseName = 'MIT License'
    licenseUrl = 'https://opensource.org/licenses/MIT'
    allLicenses = ["MIT"]
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode project.ext.libraryVersionCode
        versionName "${project.ext.libraryVersion}"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

android.libraryVariants.all { variant ->
    task("jar${variant.name}", type: Jar) {
        description "Bundles compiled .class files into a JAR file for $variant.name."
        dependsOn variant.javaCompile
        from variant.javaCompile.destinationDir
        archiveName "cc-android-sdk-${project.ext.libraryVersion}.jar"
        exclude '**/R.class', '**/R$*.class', '**/R.html', '**/R.*.html'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.google.android.gms:play-services-ads:10.2.6'
}

apply from: 'maven-install.gradle'

if(project.file('bintray.properties').exists()) {
    apply from: 'bintray-push.gradle'
}